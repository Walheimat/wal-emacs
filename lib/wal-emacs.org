#+TITLE: Emacs
#+AUTHOR: @Walheimat
#+PROPERTY: header-args:emacs-lisp :tangle (wal-tangle-target)
#+TAGS: { package : builtin(b) melpa(m) gnu(e) nongnu(n) git(g) }

Everything that has to do with Emacs-y stuff.

This is a combination of configurations for built-in packages some
external ones.

* Header
:PROPERTIES:
:VISIBILITY: folded
:END:

#+BEGIN_SRC emacs-lisp
;;; wal-emacs.el --- Emacs. -*- lexical-binding: t -*-

;;; Commentary:
;;
;; Provide Emacs settings/configurations.

;;; Code:

(eval-when-compile
  (require 'wal-useful nil t)
  (require 'wal-key-bindings nil t)
  (require 'wal-bridge nil t))

(declare-function consult--buffer-query "ext:consult.el")
(declare-function pdf-tools-install "ext:pdf-tools.el")
(declare-function wal-insert "wal-useful.el")
(declare-function wal-switch-to-buffer-obeying-display-actions "wal-useful.el")
(declare-function wal-modern-emacs-p "wal-useful.el")
(declare-function wdb-faraway "wal-useful.el")
(declare-function wdb-nearby "wal-useful.el")

(defvar wal-triple-minus-map)
(defvar wal-fix-syntax-checker)

(wal-on-boot emacs
  (wdb-faraway "\\*Messages\\*")
  (wdb-faraway 'help-mode)
  (wdb-faraway 'shortdoc-mode)
  (wdb-faraway 'debug-mode))
#+END_SRC

* Packages

** abbrev                                                           :builtin:
:PROPERTIES:
:UNNUMBERED: t
:END:

Automatic quick expansion.

#+BEGIN_SRC emacs-lisp
(use-package abbrev
  :custom
  (save-abbrevs t)

  :delight " abb")
#+END_SRC

** async                                                                :gnu:
:PROPERTIES:
:UNNUMBERED: t
:END:

Execute IO actions asynchronously. This is mainly for moving files in
Dired.

#+BEGIN_SRC emacs-lisp
(use-package async
  :defer 2
  :after dired

  :config
  (dired-async-mode 1)

  :functions (dired-async-mode))
#+END_SRC

** bookmark                                                         :builtin:
:PROPERTIES:
:UNNUMBERED: t
:END:

Leave a bookmark why don't you. Binds the keymap and sets up
annotations.

#+BEGIN_SRC emacs-lisp
(use-package bookmark
  :init
  (that-key "bookmark" :key "C-c m")

  :config
  (wdb-nearby "\\*Bookmark Annotation\\*" :side 'left :no-other t)

  :custom
  (bookmark-use-annotations t)
  (bookmark-menu-confirm-deletion t)
  (bookmark-fringe-mark nil)

  :bind-keymap
  (("C-c m" . bookmark-map))

  :bind
  (:map bookmark-map
   ("l" . bookmark-bmenu-list)
   ("L" . bookmark-load)))
#+END_SRC

** calc                                                             :builtin:
:PROPERTIES:
:UNNUMBERED: t
:END:

Quick calculations.

#+BEGIN_SRC emacs-lisp
(use-package calc
  :bind
  ("C-c q" . quick-calc))
#+END_SRC

** compile                                                          :builtin:
:PROPERTIES:
:UNNUMBERED: t
:END:

Show compilations in a pop-up and scroll output.

#+begin_src emacs-lisp
(defun wal-compilation-buffer-p (buffer)
  "Check if buffer BUFFER is a compilation buffer."
  (with-current-buffer buffer
    (derived-mode-p 'compilation-mode 'comint-mode)))

(defun wal-consult-compilation-buffer--query ()
  "Get compilation buffer names."
  (consult--buffer-query
   :sort 'visibility
   :as #'buffer-name
   :predicate #'wal-compilation-buffer-p))

(use-package compile
  :hook
  (compilation-filter . ansi-color-compilation-filter)

  :config
  (wdb-faraway "\\*compilation")

  (with-eval-after-load 'consult
    (defvar wal-consult--source-compilation-buffer
      (list :name "Compilations"
            :narrow ?c
            :category 'buffer
            :history 'buffer-name-history
            :action #'wal-switch-to-buffer-obeying-display-actions
            :items #'wal-consult-compilation-buffer--query))

     (wal-insert
      'consult-buffer-sources
      'consult--source-buffer
      'wal-consult--source-compilation-buffer
      :quiet t))

  :custom
  (compilation-scroll-output t)
  (compilation-max-output-line-length nil)

  :bind
  (("C-c r" . recompile)
   ("C-c i" . compile)))
#+END_SRC

** diff-mode                                                        :builtin:
:PROPERTIES:
:UNNUMBERED: t
:END:

Effing diffing.

#+BEGIN_SRC emacs-lisp
(use-package diff-mode
  :bind
  ;; Remove `diff-goto-source'.
  (:map diff-mode-shared-map
   ("o" . nil)))
#+END_SRC

** doc-view                                                         :builtin:
:PROPERTIES:
:UNNUMBERED: t
:END:

#+begin_src emacs-lisp
(defvar-local wal-doc-view-page-offset 0)
(put 'wal-doc-view-page-offset 'safe-local-variable #'integerp)

(defun wal-with-page-offset (goto page)
  "Go to PAGE with an offset.

GOTO is the advised function

This function only applies the offset if `this-command' is
`doc-view-goto-page' to avoid offsetting going to a previous or
next page."
  (let ((offset (if (eq 'doc-view-goto-page this-command)
                    (+ page wal-doc-view-page-offset)
                  page)))

    (funcall-interactively goto offset)))

(use-package doc-view
  :config
  (advice-add
   'doc-view-goto-page :around
   #'wal-with-page-offset))
#+end_src

** eldoc                                                            :builtin:
:PROPERTIES:
:UNNUMBERED: t
:END:

Contextual information. Package =lsp-mode= use =eldoc= extensively.

#+BEGIN_SRC emacs-lisp
(use-package eldoc
  :custom
  (eldoc-idle-delay 0.2)
  (eldoc-echo-area-prefer-doc-buffer 'maybe)
  (eldoc-echo-area-display-truncation-message nil)

  :delight " eld")
#+END_SRC

** follow                                                           :builtin:
:PROPERTIES:
:UNNUMBERED: t
:END:

Follow me around.

#+begin_src emacs-lisp
(use-package follow
  :custom
  (follow-mode-line-text " flw"))
#+end_src

** helpful                                                            :melpa:
:PROPERTIES:
:UNNUMBERED: t
:END:

Let's try to be even more =helpful=. This provides alternate version
of various =describe= commands to provide more information.

#+BEGIN_SRC emacs-lisp
(use-package helpful
  :wal-ways t

  :config
  (wdb-faraway 'helpful-mode)

  :custom
  (helpful-max-buffers 3)

  :bind
  (([remap describe-command] . helpful-command)
   ([remap describe-function] . helpful-function)
   ([remap describe-key] . helpful-key)
   ([remap describe-variable] . helpful-variable)
   ([remap describe-symbol] . helpful-symbol)

   :map help-map
   ("M" . helpful-macro)
   ("C-x" . helpful-kill-buffers)

   :map helpful-mode-map
   ("k" . kill-buffer-and-window))

  :defines (helpful-mode-map))
#+END_SRC

** hl-line                                                          :builtin:
:PROPERTIES:
:UNNUMBERED: t
:END:

Highlighting the current line.

#+BEGIN_SRC emacs-lisp
(use-package hl-line
  :hook
  ((tablist-minor-mode
    tabulated-list-mode
    dired-mode)
   . hl-line-mode))
#+END_SRC

** kmacro                                                           :builtin:
:PROPERTIES:
:UNNUMBERED: t
:END:

Some people need macros, okay?

#+BEGIN_SRC emacs-lisp
(defun wal-kmacro (arg)
  "Toggle kmacro recording with ARG."
  (interactive "P")

  (cond
   (defining-kbd-macro (kmacro-end-macro arg))
   (t (kmacro-start-macro arg))))

(use-package kmacro
  :general
  (editor "k" 'wal-kmacro))
#+END_SRC

** pulse                                                            :builtin:
:PROPERTIES:
:UNNUMBERED: t
:END:

Highlight lines.

#+BEGIN_SRC emacs-lisp
(defun wal-lighthouse ()
  "Do a heavy `pulse-momentary-highlight-one-line'.

This just means increasing duration, delay, size and brightness."
  (interactive)

  (defvar pulse-iterations)
  (let ((pulse-iterations 30))

    (pulse-momentary-highlight-one-line (point) 'cursor)))

(use-package pulse
  :bind
  ("C-c p" . wal-lighthouse))
#+END_SRC

** register                                                         :builtin:
:PROPERTIES:
:UNNUMBERED: t
:END:

No offender.

#+BEGIN_SRC emacs-lisp
(defun wal-clear-registers ()
  "Clear all registers."
  (interactive)

  (setq register-alist nil))

(use-package register
  :custom
  (register-preview-delay 0.8)

  :bind
  (:map wal-triple-minus-map
   ("r" . wal-clear-registers))

  :wal-bind
  (("p" . point-to-register)
   ("y" . window-configuration-to-register)
   ("<SPC>" . jump-to-register)))
#+END_SRC

** repeat                                                           :builtin:
:PROPERTIES:
:UNNUMBERED: t
:END:

#+begin_src emacs-lisp
(use-package repeat
  :custom
  (repeat-exit-key (kbd "<return>"))
  (repeat-exit-timeout 5))
#+end_src

** shell                                                            :builtin:
:PROPERTIES:
:UNNUMBERED: t
:END:

I'd like to kill shells without a process quickly.

#+BEGIN_SRC emacs-lisp
(use-package shell
  :general
  (general-define-key
   :keymaps '(shell-mode-map comint-mode-map)
   :predicate '(wal-dead-shell-p)
   "k" 'kill-buffer-and-window))
#+END_SRC

** text-mode                                                        :builtin:
:PROPERTIES:
:UNNUMBERED: t
:END:

No double spaces in sentences.

#+BEGIN_SRC emacs-lisp
(use-package text-mode
  :init
  (harpoon text-mode
    :flat t
    :functions
    (wal-text-mode-flyspell-commit-messages))

  :custom
  (sentence-end-double-space nil))

(defun wal-text-mode-flyspell-commit-messages ()
  "Activate `flyspell-mode' when editing commit messages."
  (when (string-match "COMMIT_EDITMSG" (buffer-name))
    (flyspell-mode)))
#+END_SRC

** pdf-tools
:PROPERTIES:
:UNNUMBERED: t
:END:

This provides better PDF editing capabilities than the built-in
=doc-view-mode=.

#+begin_src emacs-lisp
(use-package pdf-tools
  :defer 3
  :after doc-view

  :config
  (pdf-tools-install))
#+end_src

* Footer
:PROPERTIES:
:VISIBILITY: folded
:END:

#+BEGIN_SRC emacs-lisp
(provide 'wal-emacs)

;;; wal-emacs.el ends here
#+END_SRC
