#+TITLE: Windows
#+AUTHOR: @Walheimat
#+PROPERTY: header-args:emacs-lisp :tangle (expand-file-name "wal-windows.el" wal-emacs-config-build-path)

Everything that has to do with windows.

* Header
:PROPERTIES:
:VISIBILITY: folded
:END:

#+BEGIN_SRC emacs-lisp
;;; wal-windows.el --- Windows. -*- lexical-binding: t -*-

;;; Commentary:
;;
;; Provide window packages.

;;; Code:

(eval-when-compile
  (require 'wal-useful nil t)
  (require 'wal-package nil t)
  (require 'wal-key-bindings nil t))

(require 'ring)
(require 'cl-macs)
(require 'subr-x)

(declare-function consult--buffer-query "ext:consult.el")
(declare-function consult--buffer-state "ext:consult.el")
#+END_SRC

* Packages

** windmove
:PROPERTIES:
:UNNUMBERED: t
:END:

#+begin_src emacs-lisp
(use-package windmove
  :hook (emacs-startup . windmove-mode)

  :custom
  (windmove-default-keybindings (cons nil '(hyper)))
  (windmove-swap-states-default-keybindings (cons nil '(hyper shift)))
  (windmove-delete-default-keybindings (cons 'none '(hyper meta)))
  (windmove-display-default-keybindings (cons nil '(hyper control))))
#+end_src

** winner
:PROPERTIES:
:UNNUMBERED: t
:END:

#+BEGIN_SRC emacs-lisp
(use-package winner
  :hook (emacs-startup . winner-mode))
#+END_SRC

** tab-bar
:PROPERTIES:
:UNNUMBERED: t
:END:

Invisible tabs.

*** Utility

#+BEGIN_SRC emacs-lisp
(defun wal-tab-bar-switch-to-buffer-tab (buffer)
  "If BUFFER is shown in a tab, switch to the tab.

Otherwise switch to the buffer normally."
  (if-let* ((tab (tab-bar-get-buffer-tab buffer))
            (tab-name (cdr-safe (assoc 'name tab))))
      (progn
        (tab-bar-switch-to-tab tab-name)
        (select-window (get-buffer-window buffer)))
    (switch-to-buffer buffer)))

(defvar wal-partial-recall--table (make-hash-table))
(defconst wal-partial-recall--history-size 10)

(defun wal-partial-recall--key (&optional tab)
  "Get the hash key of TAB."
  (when-let* ((tab (or tab (tab-bar--current-tab)))
              (key (alist-get 'wpr tab)))
    key))

(defun wal-partial-recall--create-hash-key (tab)
  "Create the key for TAB.

This uses a message digest of the tab, a random number, the Emacs
PID and `recent-keys' vector."
  (let ((object (format "%s%s%s%s" tab (random) (emacs-pid) (recent-keys))))

    (md5 object)))

(defun wal-partial-recall--on-create (tab)
  "Equip TAB with a unique hash key."
  (let ((key (wal-partial-recall--create-hash-key tab))
        (state (cdr tab)))

    (setcdr tab (push (cons 'wpr key) state))))

(defun wal-partial-recall--remember (&optional buffer)
  "Remember the BUFFER for this tab.

If no buffer is passed, the current buffer is used."
  (when-let* ((tab-key (wal-partial-recall--key))

              (buffer (or buffer (current-buffer)))

              (table wal-partial-recall--table)
              (history (gethash
                         tab-key
                         table
                         (make-ring wal-partial-recall--history-size))))

    (unless (ring-member history buffer)
      (ring-insert history buffer)
      (puthash tab-key history table))))

(defun wal-partial-recall--forget ()
  "Forget the current buffer."
  (let* ((buffer (current-buffer))

         (table wal-partial-recall--table)
         (maybe-remove (lambda (tab-name history)
                         (when-let ((index (ring-member history buffer)))

                           (ring-remove history index)
                           (puthash tab-name history table)))))

    (maphash maybe-remove table)))

(defun wal-partial-recall--on-close (tab only)
  "Remove TAB from table if it is not the ONLY one."
  (when-let* ((tab-key (wal-partial-recall--key tab))
              (table wal-partial-recall--table))

    (when (and (not only)
               (gethash tab-key table))
      (remhash tab-key table))))

(defun wal-partial-recall--history ()
  "Get the buffer history for the current tab."
  (when-let* ((tab-key (wal-partial-recall--key))

              (table wal-partial-recall--table)
              (history (gethash tab-key table)))
    history))

(defun wal-partial-recall--current-p (buffer)
  "Check if BUFFER belongs to the current tab."
  (when-let ((history (wal-partial-recall--history)))

    (ring-member history buffer)))

(defun wal-partial-recall--has-buffers-p ()
  "Check if there are buffers associated with the current tab."
  (when-let ((history (wal-partial-recall--history)))

    (not (ring-empty-p history))))

(defun wal-partial-recall--known-buffer-p (buffer)
  "Check if BUFFER is recalled at all."
  (let ((known (cl-loop for _k being the hash-keys of wal-partial-recall--table
                        using (hash-values v)
                        append (ring-elements v))))
    (memq buffer known)))

(defun wal-partial-recall--maybe-remember ()
  "Remember the current file buffer if it's unknown."
  (and-let* ((buffer (nth 0 (buffer-list)))
             (file-name (buffer-file-name buffer))
             (unknown (not (wal-partial-recall--known-buffer-p buffer))))
    (wal-partial-recall--remember buffer)))

(defun wal-partial-recall--on-frame-delete (frame)
  "Clear hashes associated with FRAME."
  (let ((tabs (funcall tab-bar-tabs-function frame)))

    (dolist (tab tabs)
      (wal-partial-recall--on-close tab nil))))

(defvar wal-consult--source-partial-recall
  (list :name "Partiall Recall"
        :narrow ?r
        :category 'buffer
        :state #'consult--buffer-state
        :history 'buffer-name-history
        :items
        #'(lambda () (consult--buffer-query :sort 'visibility
                                       :predicate #'wal-partial-recall--current-p
                                       :as #'buffer-name)))
  "Buffers that are recalled from the current tab.")

(wal-define-init-setup tab-bar
  "Set up the original tab."
  :always
  ((when-let* ((mode tab-bar-mode)
               (tabs (funcall tab-bar-tabs-function))
               (original (nth 0 tabs)))

   (unless (wal-partial-recall--key original)
     (wal-partial-recall--on-create original)))))
#+END_SRC

*** Configuration

#+begin_src emacs-lisp
(use-package tab-bar
  :hook
  ((emacs-startup . tab-bar-mode)
   (find-file . wal-partial-recall--remember)
   (kill-buffer . wal-partial-recall--forget)
   (buffer-list-update . wal-partial-recall--maybe-remember))

  :config
  (with-eval-after-load 'consult
    (wal-insert-after
     'consult-buffer-sources
     'consult--source-buffer
     'wal-consult--source-partial-recall))

  (add-to-list 'tab-bar-tab-pre-close-functions #'wal-partial-recall--on-close)
  (add-to-list 'tab-bar-tab-post-open-functions #'wal-partial-recall--on-create)
  (add-to-list 'delete-frame-functions #'wal-partial-recall--on-frame-delete)

  :custom
  (tab-bar-show nil)

  (tab-bar-new-tab-choice #'wal-dashboard-get-buffer)
  (tab-bar-new-tab-group nil)

  :wal-bind
  ("o" . tab-switch))
#+end_src

* Footer
:PROPERTIES:
:VISIBILITY: folded
:END:

#+BEGIN_SRC emacs-lisp
(provide 'wal-windows)

;;; wal-windows.el ends here
#+END_SRC
